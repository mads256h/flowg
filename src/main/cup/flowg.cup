package org.flowsoft.flowg;

import java.util.ArrayList;
import java_cup.runtime.*;
import org.flowsoft.flowg.nodes.*;
import org.flowsoft.flowg.nodes.base.*;
import org.flowsoft.flowg.nodes.controlflow.*;
import org.flowsoft.flowg.nodes.functions.*;
import org.flowsoft.flowg.nodes.math.functions.*;
import org.flowsoft.flowg.nodes.math.operators.*;

terminal TypeNode TYPE;

terminal IdentifierNode IDENTIFIER;

terminal NumberLiteralNode NUMBER_LITERAL;
terminal BooleanLiteralNode BOOLEAN_LITERAL;

// Builtin functions
terminal MOVE;
terminal LINE;
terminal CW_ARC;
terminal CCW_ARC;

// Math builtins
terminal SQRT;
terminal SIN;
terminal COS;
terminal TAN;
terminal ARCSIN;
terminal ARCCOS;
terminal ARCTAN;


// ()
terminal L_PAREN;
terminal R_PAREN;

// {}
terminal L_BRACKET;
terminal R_BRACKET;

// []
terminal L_SQUARE_BRACKET;
terminal R_SQUARE_BRACKET;

terminal ASSIGNMENT;
terminal SEMICOLON;
terminal COMMA;
terminal DOT;

// Arithmic operators
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal POWER;

// Boolean operators
terminal GREATER_THAN;
terminal LESS_THAN;
terminal EQUALS;
terminal GREATER_THAN_EQUALS;
terminal LESS_THAN_EQUALS;
terminal AND;
terminal OR;
terminal NOT;

// Control flow
terminal FOR;
terminal TO;
terminal RETURN;
terminal IF;
terminal ELSE;


non terminal StatementListNode program;
non terminal StatementListNode stmt_list;
non terminal ArrayList<StatementNode> stmt_list_impl;
non terminal StatementNode stmt;

non terminal DeclarationNode decl;
non terminal AssignmentNode assign;

// Builtin functions
non terminal MoveNode move;
non terminal LineNode line;
non terminal CWArcNode cw_arc;
non terminal CCWArcNode ccw_arc;

// Math builtins
non terminal SqrtNode sqrt;
non terminal SinNode sin;
non terminal CosNode cos;
non terminal TanNode tan;
non terminal ArcsinNode arcsin;
non terminal ArccosNode arccos;
non terminal ArctanNode arctan;

// User supplied functions
non terminal FunctionDefinitionNode func_def;
non terminal FunctionCallNode func_call;

non terminal ActualParameterListNode a_param_list;
non terminal ArrayList<ExpressionNode> a_param_list_impl;

non terminal FormalParameterListNode f_param_list;
non terminal ArrayList<FormalParameterNode> f_param_list_impl;
non terminal FormalParameterNode f_param;

non terminal ExpressionNode expr;
non terminal PointNode point;
non terminal PointEntryNode point_entry;

// Control flow
non terminal ForToNode for_to;
non terminal ReturnNode return;
non terminal IfElseNode if_else;

precedence left NOT;
precedence left AND, OR;
precedence left EQUALS, GREATER_THAN, LESS_THAN, GREATER_THAN_EQUALS, LESS_THAN_EQUALS;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left POWER;
precedence left DOT;

start with program;

program ::= stmt_list:l
            {: RESULT = l; :};

stmt_list ::= stmt_list_impl:l
            {: RESULT = new StatementListNode(l, lxleft, lxright); :};

stmt_list_impl ::= stmt_list_impl:l stmt:s SEMICOLON
            {: l.add(s); RESULT = l; :}
            | stmt:s SEMICOLON
            {: var list = new ArrayList<StatementNode>(); list.add(s); RESULT = list; :};

stmt ::=
        // Variable declaration and assignment
        decl:d
        {: RESULT = d; :}
        | assign:a
        {: RESULT = a; :}

        // Builtin functions
        | move:m
        {: RESULT = m; :}
        | line:l
        {: RESULT = l; :}
        | cw_arc:c
        {: RESULT = c; :}
        | ccw_arc:c
        {: RESULT = c; :}

        // User supplied functions
        | func_def:d
        {: RESULT = d; :}
        | func_call:c
        {: RESULT = c; :}

        // Control flow
        | for_to:f
        {: RESULT = f; :}
        | return:r
        {: RESULT = r; :}
        | if_else:i
        {: RESULT = i; :}
        ;

decl ::= TYPE:t IDENTIFIER:i ASSIGNMENT expr:e
        {: RESULT = new DeclarationNode(t, i, e, txleft, exright); :};

assign ::= IDENTIFIER:i ASSIGNMENT expr:e
        {: RESULT = new AssignmentNode(i, e, ixleft, exright); :};

// Builtin functions
move ::= MOVE:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new MoveNode(p, bxleft, exright); :};

line ::= LINE:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new LineNode(p, bxleft, exright); :};

cw_arc ::= CW_ARC:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new CWArcNode(p, bxleft, exright); :};

ccw_arc ::= CCW_ARC:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new CCWArcNode(p, bxleft, exright); :};

// User defined functions
func_def ::= TYPE:t IDENTIFIER:i L_PAREN f_param_list:p R_PAREN L_BRACKET stmt_list:l R_BRACKET:e
        {: RESULT = new FunctionDefinitionNode(t, i, p, l, txleft, exright); :};

func_call ::= IDENTIFIER:i L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new FunctionCallNode(i, p, ixleft, exright); :};

a_param_list ::= a_param_list_impl:l
        {: RESULT = new ActualParameterListNode(l, lxleft, lxright); :};

a_param_list_impl ::= a_param_list_impl:l COMMA expr:e
        {: l.add(e); RESULT = l; :}
        | expr:e
        {: var l = new ArrayList<ExpressionNode>(); l.add(e); RESULT = l; :}
        |
        {: RESULT = new ArrayList<ExpressionNode>(); :};


f_param_list ::= f_param_list_impl:l
        {: RESULT = new FormalParameterListNode(l, lxleft, lxright); :};


f_param_list_impl ::= f_param_list_impl:l COMMA f_param:p
        {: l.add(p); RESULT = l; :}
        | f_param:p
        {: var l = new ArrayList<FormalParameterNode>(); l.add(p); RESULT = l; :}
        |
        {: RESULT = new ArrayList<FormalParameterNode>(); :};

f_param ::= TYPE:t IDENTIFIER:i
        {: RESULT = new FormalParameterNode(t, i, txleft, ixright); :};

// Control flow
for_to ::= FOR:b L_PAREN decl:d TO expr:e R_PAREN L_BRACKET stmt_list:l R_BRACKET:f
        {: RESULT = new ForToNode(d, e, l, bxleft, fxright); :};

return::= RETURN:b expr:e
        {: RESULT = new ReturnNode(e, bxleft, exright); :}
        | RETURN:b
        {: RESULT = new ReturnNode(null, bxleft, bxright); :};

if_else ::= IF:b L_PAREN expr:e R_PAREN L_BRACKET stmt_list:l1 R_BRACKET:f
        {: RESULT = new IfElseNode(e, l1, null, bxleft, fxright); :}
        | IF:b L_PAREN expr:e R_PAREN L_BRACKET stmt_list:l1 R_BRACKET ELSE L_BRACKET stmt_list:l2 R_BRACKET:f
        {: RESULT = new IfElseNode(e, l1, l2, bxleft, fxright); :};

expr ::=
        // Literals
        NUMBER_LITERAL:n
        {: RESULT = n; :}
        | BOOLEAN_LITERAL:b
        {: RESULT = b; :}
        | point:p
        {: RESULT = p; :}

        // Point entry
        | point_entry:e
        {: RESULT = e; :}

        // Parens
        // TODO: fix left and right
        | L_PAREN expr:e R_PAREN
        {: RESULT = e; :}

        // Arithmic operators
        | expr:e1 PLUS expr:e2
        {: RESULT = new PlusExpressionNode(e1, e2, e1xleft, e2xright); :}
        | expr:e1 MINUS expr:e2
        {: RESULT = new MinusExpressionNode(e1, e2, e1xleft, e2xright); :}
        | expr:e1 TIMES expr:e2
        {: RESULT = new TimesExpressionNode(e1, e2, e1xleft, e2xright); :}
        | expr:e1 DIVIDE expr:e2
        {: RESULT = new DivideExpressionNode(e1, e2, e1xleft, e2xright); :}
        | expr:e1 POWER expr:e2
        {: RESULT = new PowerExpressionNode(e1, e2, e1xleft, e2xright); :}
        | IDENTIFIER:i
        {: RESULT = new IdentifierExpressionNode(i, ixleft, ixright); :}
        | func_call:c
        {: RESULT = c; :}

        // Math builtins
        | sqrt:s
        {: RESULT = s; :}
        | sin:s
        {: RESULT = s; :}
        | cos:c
        {: RESULT = c; :}
        | tan:t
        {: RESULT = t; :}
        | arcsin:s
        {: RESULT = s; :}
        | arccos:c
        {: RESULT = c; :}
        | arctan:t
        {: RESULT = t; :}

        // Boolean Expressions
        | expr:b1 GREATER_THAN expr:b2
        {: RESULT = new GreaterThanExpressionNode(b1, b2, b1xleft, b2xright); :}
        | expr:b1 LESS_THAN expr:b2
        {: RESULT = new LessThanExpressionNode(b1, b2, b1xleft, b2xright); :}
        | expr:e1 EQUALS expr:e2
        {: RESULT = new EqualsExpressionNode(e1, e2, e1xleft, e2xright); :}
        | expr:b1 GREATER_THAN_EQUALS expr:b2
        {: RESULT = new GreaterThanEqualsExpressionNode(b1, b2, b1xleft, b2xright); :}
        | expr:b1 LESS_THAN_EQUALS expr:b2
        {: RESULT = new LessThanEqualsExpressionNode(b1, b2, b1xleft, b2xright); :}
        | expr:b1 AND expr:b2
        {: RESULT = new AndExpressionNode(b1, b2, b1xleft, b2xright); :}
        | expr:b1 OR expr:b2
        {: RESULT = new OrExpressionNode(b1, b2, b1xleft, b2xright); :}
        | NOT:b expr:e
        {: RESULT = new NotExpressionNode(e, bxleft, exright); :}
        ;

// Math builtins
sqrt ::= SQRT:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new SqrtNode(p, bxleft, exright); :};

sin ::= SIN:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new SinNode(p, bxleft, exright); :};

cos ::= COS:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new CosNode(p, bxleft, exright); :};

tan ::= TAN:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new TanNode(p, bxleft, exright); :};

arcsin ::= ARCSIN:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new ArcsinNode(p, bxleft, exright); :};

arccos ::= ARCCOS:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new ArccosNode(p, bxleft, exright); :};

arctan ::= ARCTAN:b L_PAREN a_param_list:p R_PAREN:e
        {: RESULT = new ArctanNode(p, bxleft, exright); :};


point ::= L_SQUARE_BRACKET:b expr:e1 COMMA expr:e2 COMMA expr:e3 R_SQUARE_BRACKET:e
        {: RESULT = new PointNode(e1, e2, e3, bxleft, exright); :};

point_entry ::= expr:e DOT IDENTIFIER:i
        {: RESULT = new PointEntryNode(e, i, exleft, ixright); :};
